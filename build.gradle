/**
 * 1.0.0 : release
 */
def ARTIFACT_VERSION = "1.0.0"
def ARTIFACT_NAME = file(".").absoluteFile.name

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

repositories {
    maven { url "http://eaglesakura.github.io/maven/" }
    mavenCentral()
}

apply plugin: 'com.android.library'

sourceCompatibility = 1.7
targetCompatibility = 1.7

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
    }

    sourceSets {
        main {
            java.srcDirs = [
                    "src/main/java", "src/main/generated/prop",
            ]
        }
    }
}

// check env
if (!hasProperty("ANDROID_SUPPORT_LIB_VERSION")) {
    ext.ANDROID_SUPPORT_LIB_VERSION = "+"
}
if (!hasProperty("ANDROID_PLAYSERVICE_LIB_VERSION")) {
    ext.ANDROID_PLAYSERVICE_LIB_VERSION = "+"
}

dependencies {
    compile 'com.eaglesakura:simple-utils:1.0.+'
    compile 'com.eaglesakura:android-simple-utils:1.0.+'
    compile 'com.eaglesakura:android-text-kvs:1.0.+'
    compile 'com.eaglesakura:android-thread:1.0.+'
    compile 'com.eaglesakura:android-canvas-graphics:1.0.+'
    compile 'com.eaglesakura:android-net:1.0.+'

    // Fixed Libs
    compile 'com.eaglesakura:onactivityresult-invoke:+'
    compile 'com.eaglesakura:small-aquery:+'

    /**
     * support libs
     */
    compile "com.android.support:support-v4:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:palette-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_LIB_VERSION}"

    /**
     * Play Service
     */
    compile "com.google.android.gms:play-services-plus:${ANDROID_PLAYSERVICE_LIB_VERSION}"
    compile "com.google.android.gms:play-services-gcm:${ANDROID_PLAYSERVICE_LIB_VERSION}"
    compile 'com.android.support:support-annotations:+'

    /**
     * Butter Knife & Icepick
     */
    compile "com.jakewharton:butterknife:7.0.+"
    compile 'frankiesardo:icepick:3.1.0'
    provided 'frankiesardo:icepick-processor:3.1.0'
}

apply plugin: 'maven'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

android.libraryVariants.all { variant ->
    if (!"release".equals(variant.name) || "".equals(ARTIFACT_VERSION)) {
        return;
    }

    task("javadoc", type: Javadoc) {
        title = "${file(".").name} ver ${ARTIFACT_VERSION}"
        description "Generates Android Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar) + files("build/intermediates/classes/${variant.name}/")
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/*Dao.java' /* ignore greenDAO */
        exclude '**/DaoMaster.java' /* ignore greenDAO */
        exclude '**/DaoSession.java' /* ignore greenDAO */
        exclude '**/R.java' /* ignore greenDAO */
    }


    task("javadocJar", type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(new File("${System.env.MAVEN_LOCAL_REPO}")))
            pom.version = ARTIFACT_VERSION
            pom.groupId = "com.eaglesakura"
            pom.artifactId = ARTIFACT_NAME
        }
    }
}

[uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2016'
        packaging 'aar'
        licenses {
            license {
                name 'MIT License'
                url "https://raw.githubusercontent.com/eaglesakura/${ARTIFACT_NAME}/master/LICENSE.txt"
            }
        }
        dependencies {
            dependency {
                groupId = 'com.google.http-client'
                artifactId = 'google-http-client-android'
                version = '1.21.+'
                scope = 'compile'
            }
        }
    }
}

task uploadJavadoc << {
    def src = file("build/docs/javadoc/");
    def dst = new File("${System.env.MAVEN_LOCAL_REPO}/doc/${ARTIFACT_NAME}/");
    dst.deleteDir();
    dst.mkdirs();

    ["/usr/bin/rsync", "-a", src.absolutePath, dst.absolutePath].execute().text;
}
